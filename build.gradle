plugins {
    id 'java'
    id 'maven-publish'
}

group = 'de.mineking'
version = '3.1.0'

var commit = getCommit()
var release = System.getenv("RELEASE") == "true"

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'net.dv8tion:JDA:5.0.0-beta.17'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'ch.qos.logback:logback-classic:1.3.8'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            url = "https://maven.mineking.dev/" + (release ? "releases" : "snapshots")
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_SECRET")
            }
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "de.mineking"
            artifactId = "DiscordUtils"
            version = release ? this.version : commit

            from components.java
        }
    }
}

test {
    useJUnitPlatform()

    compileJava {
        options.compilerArgs << '-parameters'
    }
}

compileJava {
    options.compilerArgs << '-parameters'
}

static String getCommit() {
    try {
        var commit = 'git rev-parse --verify --short HEAD'.execute().text.trim()
        return commit.isEmpty() ? 'unknown' : commit
    } catch(IOException e) {
        throw new RuntimeException(e)
    }
}
